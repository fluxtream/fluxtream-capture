apply plugin: "com.android.application"

android {
	compileSdkVersion rootProject.ext.compileSdkVersion
	buildToolsVersion rootProject.ext.buildToolsVersion

	defaultConfig {
		applicationId "io.trigger.forge.android.inspector"
		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
		compileOptions rootProject.ext.compileOptions
	}

	productFlavors {
        vanilla {
        }
        crosswalk {
			minSdkVersion 14
			ndk {
				abiFilters "armeabi-v7a", ""
			}
        }
    }

	sourceSets {
		main {
			manifest.srcFile "AndroidManifest.xml"
			java.srcDirs = ["src"]
			resources.srcDirs = ["src"]
			aidl.srcDirs = ["src"]
			renderscript.srcDirs = ["src"]
			res.srcDirs = ["res"] + new File("ForgeInspector").listFiles().findAll { 
				it.isDirectory() and it.name.startsWith("res_module_") 
			}.collect { 
				println "Added module resource directory: " + it
				it.name 
			}
			assets.srcDirs = ["assets"]
		}
		instrumentTest.setRoot("tests")
	}

	buildTypes {
		debug rootProject.ext.debug
		release rootProject.ext.release << {
			proguardFile getDefaultProguardFile("proguard-android.txt")
			proguardFile "proguard-project.txt"
		}
	}

	lintOptions {
		abortOnError false
	}

	aaptOptions {
		noCompress "xml", "png"
	}
}

dependencies {
	crosswalkCompile "org.xwalk:xwalk_core_library:" + rootProject.ext.crosswalkVersion
	crosswalkCompile project(path: ":ForgeModule", configuration: "crosswalkDebug")
	vanillaCompile project(path: ":ForgeModule", configuration: "vanillaDebug") 

	compile fileTree(dir: "libs", include: ["*.jar"], exclude: ["forgecore.jar", "guava-13.0.1.jar"]) 
}

repositories{
	flatDir {
		dirs "../ForgeModule/libs"
	}
}